ext{
    versions = [:]
    libraries = [:]
}

versions.robovm = "2.3.18"
versions.junit = "4.11"
versions.jnigen = "2.3.1"

libraries.robovm = [
    "com.mobidevelop.robovm:robovm-rt:${versions.robovm}",
    "com.mobidevelop.robovm:robovm-objc:${versions.robovm}",
    "com.mobidevelop.robovm:robovm-cocoatouch:${versions.robovm}"
]

libraries.junit = [
    "junit:junit:${versions.junit}"
]

libraries.jnigen = [
    "com.badlogicgames.gdx:gdx-jnigen:$versions.jnigen"
]

allprojects{
    apply plugin: 'maven-publish'
    group = 'com.github.zxtej'
    version = '1.0'

    buildscript{
        repositories{
            mavenCentral()
            google()
            //maven{ url 'https://jitpack.io' }
        }

        dependencies{
            classpath 'com.badlogicgames.gdx:gdx-jnigen-gradle:$versions.jnigen'
        }
    }

    repositories{
        mavenCentral()
        google()
        maven{ url 'https://jitpack.io' }
    }

    ext.aproj = { String module ->
        if(findProject(module) != null){
            return project(module)
        }else{
            return project(":Arc" + module)
        }
    }

    tasks.withType(JavaCompile){
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
        options.encoding = "UTF-8"
    }
}

subprojects{
    apply plugin: "java-library"

    compileJava{
        options.fork = true
        options.incremental = true
    }

    java{
        withJavadocJar()
        withSourcesJar()
    }

    publishing{
        publications{
            maven(MavenPublication){
                from components.java
            }
        }
    }
}
